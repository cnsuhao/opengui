# Build Script for Renderer_Ogre
import os
import fnmatch

Import('platform')
Import('debug')
Import('base_env')
env = base_env.Copy()

# Source files (and yes, this is indeed much like cheating)
files = os.listdir(".")
Source = fnmatch.filter(files,"*.cpp")

CPPPATH = """
	#/OpenGUI
	${OGRE_HOME}/include
	"""
LIBPATH_D = """
	#/lib
	${OGRE_HOME}/lib
	"""
	
LIBPATH_R = """
	#/lib
	${OGRE_HOME}/lib
	"""

LIBS_D = """
	OpenGUI_d
	OgreMain_d
	"""

LIBS_R = """
	OpenGUI
	OgreMain
	"""

OUTFILE = 'Renderer_Ogre'



################################################################

OUTFILE_orig = OUTFILE


if debug:
	OUTFILE = OUTFILE + '_d'
	libpath = Split(LIBPATH_D)
	libs = Split(LIBS_D)
else:
	libpath = Split(LIBPATH_R)
	libs = Split(LIBS_R)



cppdefine = ['_USRDLL', 'OGR_OGRE_EXPORTS']
env.Append(CPPDEFINES = cppdefine)
env.Append(CPPPATH = Split(CPPPATH))
env.Append(LIBPATH = libpath)
env.Append(LIBS = libs)


env['PDB'] = OUTFILE + '.pdb'


lib = env.SharedLibrary( OUTFILE, Source )


if debug and platform == "win32":
	env.SideEffect(OUTFILE + '.ilk', lib)
	Clean(lib, OUTFILE + '.ilk')

final = []
if debug:
	final += env.Install('../bin/debug', lib )
else:
	final += env.Install('../bin/release', lib )
final += env.Install('../lib', OUTFILE + '.lib')
Alias('renderer_ogre',final)
Alias('ogre',final)
Alias('all',final)
